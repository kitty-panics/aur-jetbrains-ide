# Maintainer: Kitty Panics

pkgbase=jetbrains-goland
pkgname=(${pkgbase}-old ${pkgbase}-jre-old)
pkgver=2021.2.3
pkgrel=4
url="https://www.jetbrains.com/go/download/other.html"
pkgdesc="A cross-platform GO IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('nspr' 'nss' 'alsa-lib')
makedepends=('rsync')
options=('!strip')
_tar_gz="goland-${pkgver}.tar.gz"
source=("https://download.jetbrains.com/go/${_tar_gz}"
        "${pkgbase}-old.desktop"
        "LICENSE")
sha256sums=('af413a8357c15ba1aebd4594a4cb9988473d1319df2c815a5697ec5ec0cc1ed8'
            '9afc6168f866c3b35860e00259e512566f2de6f7d6ad6d9fa511dbea60e34ffb'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"-old/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"-old
}

package_jetbrains-goland-old() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-goland-jre-old: JetBrains custom Java Runtime (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-goland-jre-old is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'go:               Golang run/debug support')

    # mkdir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr
    # bin
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_jetbrains-goland-jre-old() {
    pkgdesc='JetBrains Runtime (JBR) for Goland - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"-old/

    # pkg files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"-old/jbr "${pkgdir}"/opt/"${pkgbase}"-old
}
