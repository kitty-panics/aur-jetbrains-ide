# Maintainer: Kitty Panics

pkgbase=jetbrains-intellij-idea
pkgname=(${pkgbase}-old ${pkgbase}-jre-old)
pkgver=2021.2.2
pkgrel=4
url="https://www.jetbrains.com/idea/download/other.html"
pkgdesc="A cross-platform Java and Kotlin IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('nspr' 'nss' 'alsa-lib')
makedepends=('rsync')
options=('!strip')
_tar_gz="ideaIU-$pkgver.tar.gz"
source=("https://download.jetbrains.com/idea/${_tar_gz}"
        "${pkgbase}-old.desktop"
        "LICENSE")
sha256sums=('36807af6a60a31590fb51f40a87c764007dae6866b05586ebb15d2d13933bde6'
            'b60dfca57c5ece49e5906fe66b0ebcb730e7e037a426d7d199893182e0ea747e'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"-old/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"-old
}

package_jetbrains-intellij-idea-old() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-intellij-idea-jre-old: JetBrains custom JRE (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-intellij-idea-jre-old is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'ant:              Build system'
                'gradle:           Build system'
                'maven:            Build system')

    # mkdir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr
    # bin
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

    # fix bugs:
    #
    # https://youtrack.jetbrains.com/issue/IDEA-185828
    chmod +x "${pkgdir}"/opt/"${pkgname}"/plugins/maven/lib/maven3/bin/mvn
    # https://bugs.archlinux.org/task/40934
    #sed -i 's/lcd/on/' "${pkgdir}"/opt/"${pkgname}"/bin/*.vmoptions
}

package_jetbrains-intellij-idea-jre-old() {
    pkgdesc='JetBrains Runtime (JBR) for IntelliJ IDEA - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"-old/

    # pkg files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"-old/jbr "${pkgdir}"/opt/"${pkgbase}"-old
}
