# Maintainer: Kitty Panics

    #> Package name
pkgbase=jetbrains-clion
pkgname=("${pkgbase}" "${pkgbase}"-jre "${pkgbase}"-clang "${pkgbase}"-cmake "${pkgbase}"-gdb "${pkgbase}"-lldb)
    #> Version
pkgver=2023.3.4
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="A cross-platform C and C++ IDE by JetBrains."
arch=('x86_64')
url="https://www.jetbrains.com/clion"
license=('custom:jetbrains')
    #> Dependencies
depends=('nspr' 'nss' 'pcre' 'alsa-lib')
makedepends=('rsync')
#optdepends=('')
    #> Package relations
#provides=("${pkgname}")
#conflicts=("${pkgname}")
    #> Others
#backup=("")
options=('!strip')
#install=xyz.install
    #> Sources
_tar_gz="CLion-${pkgver}.tar.gz"
source=("https://download.jetbrains.com/cpp/${_tar_gz}"
        "${pkgbase}.desktop"
        "LICENSE")
noextract=("${_tar_gz}")
    #> Integrity
sha256sums=('SKIP'
            '51c1091a54406a7504d02b737906046dde5d67ba1310ce54ef8530f14d63cc9d'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"
}

package_jetbrains-clion() {
    #> Dependencies
    optdepends=('jetbrains-clion-jre:   JetBrains custom Java Runtime (Recommended)'
                'jetbrains-clion-clang: JetBrains packaged LLVM compiler'
                'jetbrains-clion-cmake: JetBrains packaged CMake build system'
                'jetbrains-clion-gdb:   JetBrains packaged GNU debugger'
                'jetbrains-clion-lldb:  JetBrains packaged LLVM debugger'
                'java-runtime:     JRE - Required if jetbrains-clion-jre is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'cmake:            Build system - Required if jetbrains-clion-cmake is not installed'
                'gdb:              GNU debugger - Required if jetbrains-clion-gdb is not installed'
                'lldb:             LLVM debugger - Required if jetbrains-clion-lldb is not installed'
                'gcc:              GNU compiler'
                'clang:            LLVM compiler'
                'biicode:          C/C++ dependency manager'
                'gtest:            C++ testing'
                'swift-language:   Swift programming language support (Also requires the plugin)'
                'doxygen:          Code documentation generation')
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    #> Others
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")

    # create dir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr \
          --exclude=/opt/"${pkgname}"/bin/clang \
          --exclude=/opt/"${pkgname}"/bin/cmake \
          --exclude=/opt/"${pkgname}"/bin/gdb \
          --exclude=/opt/"${pkgname}"/bin/lldb
    # /usr/bin/
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons/hicolor
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_jetbrains-clion-jre() {
    #> Generic
    pkgdesc="JetBrains Runtime (JBR) for CLion - a patched JRE"
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/jbr "${pkgdir}"/opt/"${pkgbase}"
}

package_jetbrains-clion-clang() {
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/clang "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-cmake() {
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/cmake "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-gdb() {
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/gdb "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-lldb() {
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/lldb "${pkgdir}"/opt/"${pkgbase}"/bin
}
