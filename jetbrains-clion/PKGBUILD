# Maintainer: Kitty Panics

pkgbase=jetbrains-clion
pkgname=(${pkgbase} ${pkgbase}-jre ${pkgbase}-clang ${pkgbase}-cmake ${pkgbase}-gdb ${pkgbase}-lldb)
pkgver=2023.3.2
pkgrel=1
url="https://www.jetbrains.com/clion/"
pkgdesc="A cross-platform C and C++ IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('nspr' 'nss' 'pcre' 'alsa-lib')
makedepends=('rsync')
options=('!strip')
_tar_gz="CLion-${pkgver}.tar.gz"
source=("https://download.jetbrains.com/cpp/${_tar_gz}"
        "${pkgbase}.desktop"
        "LICENSE")
sha256sums=('SKIP'
            '51c1091a54406a7504d02b737906046dde5d67ba1310ce54ef8530f14d63cc9d'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"
}

package_jetbrains-clion() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-clion-jre:   JetBrains custom Java Runtime (Recommended)'
                'jetbrains-clion-clang: JetBrains packaged LLVM compiler'
                'jetbrains-clion-cmake: JetBrains packaged CMake build system'
                'jetbrains-clion-gdb:   JetBrains packaged GNU debugger'
                'jetbrains-clion-lldb:  JetBrains packaged LLVM debugger'
                'java-runtime:     JRE - Required if jetbrains-clion-jre is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'cmake:            Build system - Required if jetbrains-clion-cmake is not installed'
                'gdb:              GNU debugger - Required if jetbrains-clion-gdb is not installed'
                'lldb:             LLVM debugger - Required if jetbrains-clion-lldb is not installed'
                'gcc:              GNU compiler'
                'clang:            LLVM compiler'
                'biicode:          C/C++ dependency manager'
                'gtest:            C++ testing'
                'swift-language:   Swift programming language support (Also requires the plugin)'
                'doxygen:          Code documentation generation')

    # create dir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr \
          --exclude=/opt/"${pkgname}"/bin/clang \
          --exclude=/opt/"${pkgname}"/bin/cmake \
          --exclude=/opt/"${pkgname}"/bin/gdb \
          --exclude=/opt/"${pkgname}"/bin/lldb
    # /usr/bin/
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons/hicolor
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_jetbrains-clion-jre() {
    pkgdesc='JetBrains Runtime (JBR) for CLion - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/jbr "${pkgdir}"/opt/"${pkgbase}"
}

package_jetbrains-clion-clang() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/clang "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-cmake() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/cmake "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-gdb() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/gdb "${pkgdir}"/opt/"${pkgbase}"/bin
}

package_jetbrains-clion-lldb() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/bin/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/bin/lldb "${pkgdir}"/opt/"${pkgbase}"/bin
}
