# Maintainer: Kitty Panics

    #> Package name
pkgbase=jetbrains-rubymine
pkgname=("${pkgbase}" "${pkgbase}"-jre)
    #> Version
pkgver=2023.3.4
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="A cross-platform Ruby IDE by JetBrains."
arch=('x86_64')
url="https://www.jetbrains.com/ruby"
license=('custom:jetbrains')
    #> Dependencies
depends=('nspr' 'nss' 'pcre' 'alsa-lib')
makedepends=('rsync')
#optdepends=('')
    #> Package relations
#provides=("${pkgname}")
#conflicts=("${pkgname}")
    #> Others
#backup=("")
options=('!strip')
#install=xyz.install
    #> Sources
_tar_gz="RubyMine-${pkgver}.tar.gz"
source=("https://download.jetbrains.com/ruby/${_tar_gz}"
        "${pkgbase}.desktop"
        "LICENSE")
noextract=("${_tar_gz}")
    #> Integrity
sha256sums=('SKIP'
            '08d68bb08aec059688753b2e33a7539006a633472ffc4bfe99b84c414a8a7759'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"
}

package_jetbrains-rubymine() {
    #> Dependencies
    optdepends=('jetbrains-rubymine-jre: JetBrains custom Java Runtime (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-rubymine-jre is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'ruby:             Ruby run/debug support')
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    #> Others
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")

    # mkdir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr
    # bin
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_jetbrains-rubymine-jre() {
    #> Generic
    pkgdesc="JetBrains Runtime (JBR) for RubyMine - a patched JRE"
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/jbr "${pkgdir}"/opt/"${pkgbase}"
}
