# Maintainer: Kitty Panics

pkgbase=jetbrains-webstorm
pkgname=(${pkgbase}-old ${pkgbase}-jre-old)
pkgver=2021.2.2
pkgrel=4
url="https://www.jetbrains.com/webstorm/download/other.html"
pkgdesc="A cross-platform Web IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('nspr' 'nss' 'alsa-lib')
makedepends=('rsync')
options=('!strip')
_tar_gz="WebStorm-${pkgver}.tar.gz"
source=("https://download.jetbrains.com/webstorm/${_tar_gz}"
        "${pkgbase}-old.desktop"
        "LICENSE")
sha256sums=('226d039746ac2a6148e7c7b7b45ae87f0080eb64ede78e85226cd9a43ea67ba8'
            '4f5eb1da9180adf28bf11db4c485aa7557f4dee9eab8f9dd02d14e28a989b809'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"-old/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"-old
}

package_jetbrains-webstorm-old() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-webstorm-jre-old: JetBrains custom Java Runtime (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-webstorm-jre-old is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'nodejs:           JavaScript run/debug support')

    # create dir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr
    # /usr/bin/
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons/hicolor
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE
}

package_jetbrains-webstorm-jre-old() {
    pkgdesc='JetBrains Runtime (JBR) for WebStorm - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"-old/
    # package files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"-old/jbr "${pkgdir}"/opt/"${pkgbase}"-old
}
