# Maintainer: Kitty Panics

pkgbase=jetbrains-rubymine
pkgname=(${pkgbase}-old ${pkgbase}-jre-old)
pkgver=2021.2.2
pkgrel=1
url="https://www.jetbrains.com/ruby/download/other.html"
pkgdesc="A cross-platform Ruby IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('giflib' 'glib2' 'libxtst')
makedepends=('rsync')
options=('!strip')
_tar_gz="RubyMine-${pkgver}.tar.gz"
install="${pkgbase}-old.install"
source=("https://download.jetbrains.com/ruby/${_tar_gz}"
        "${pkgbase}-old.desktop"
        "${pkgbase}-old.install"
        "LICENSE")
sha256sums=('979745e52bdef91ed2ea46f392ba4b838849fbc3ec2ce0607ca7087bdc5479d3'
            '39ff033f451cb1f12b61370dadae3d374c364adab1c56f9e1324317d6a5aa7f5'
            'f2b528f523962d0ec4511e3fa4a6d2e5b6cf959da8547017b32c6fed6f06821a'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    rm -rf "${srcdir}/opt"
    mkdir -p "${srcdir}/opt/${pkgbase}-old"
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}/opt/${pkgbase}-old"
}

package_jetbrains-rubymine-old() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-rubymine-jre-old: JetBrains custom Java Runtime (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-rubymine-jre-old is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'ruby:             Ruby run/debug support')

    # create dir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}/opt" "${pkgdir}" \
          --exclude=/opt/${pkgname}/jbr \
    # /usr/bin/
    ln -s "/opt/${pkgname}/bin/${pkgbase##*-}.sh" \
          "${pkgdir}/usr/bin/${pkgname}"

    # applications
    install -Dm644 "${srcdir}/${pkgname}.desktop" \
                   "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    # icons/hicolor
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.png" \
                   "$pkgdir/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.svg" \
                   "$pkgdir/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
    # pixmaps
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.svg" \
                   "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
    # licenses
    install -Dm644 "${srcdir}/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_jetbrains-rubymine-jre-old() {
    pkgdesc='JetBrains Runtime (JBR) for RubyMine - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}/opt/${pkgbase}-old/"
    # package files
    rsync -rtl "${srcdir}/opt/${pkgbase}-old/jbr" "${pkgdir}/opt/${pkgbase}-old"
}
