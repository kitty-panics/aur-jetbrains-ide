# Maintainer: Kitty Panics

    #> Package name
pkgbase=jetbrains-intellij-idea
pkgname=("${pkgbase}" "${pkgbase}"-jre)
    #> Version
pkgver=2024.1.3
pkgrel=1
#epoch=1
    #> Generic
pkgdesc="A cross-platform Java and Kotlin IDE by JetBrains."
arch=('x86_64')
url="https://www.jetbrains.com/idea"
license=('custom:jetbrains')
    #> Dependencies
depends=('nspr' 'nss' 'pcre' 'alsa-lib')
makedepends=('rsync')
#optdepends=('')
    #> Package relations
#provides=("${pkgname}")
#conflicts=("${pkgname}")
    #> Others
#backup=("")
options=('!strip')
#install=xyz.install
    #> Sources
_tar_gz="ideaIU-$pkgver.tar.gz"
source=("https://download.jetbrains.com/idea/${_tar_gz}"
        "${pkgbase}.desktop"
        "LICENSE")
noextract=("${_tar_gz}")
    #> Integrity
sha256sums=('SKIP'
            '38a58a300937edc825463f9e4a9d43e782276e9979606046c8be9d466ea5a594'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"
}

package_jetbrains-intellij-idea() {
    #> Dependencies
    optdepends=('jetbrains-intellij-idea-jre: JetBrains custom JRE (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-intellij-idea-jre is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'ant:              Build system'
                'gradle:           Build system'
                'maven:            Build system')
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    #> Others
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")

    # mkdir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt "${pkgdir}" \
          --exclude=/opt/"${pkgname}"/jbr
    # bin
    ln -s /opt/"${pkgname}"/bin/"${pkgbase##*-}".sh \
          "${pkgdir}"/usr/bin/"${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}"/"${pkgname}".desktop \
                   "${pkgdir}"/usr/share/applications/"${pkgname}".desktop
    # doc
    ln -s /opt/"${pkgname}"/help \
          "${pkgdir}"/usr/share/doc/"${pkgname}"
    # icons
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".png \
                   "${pkgdir}"/usr/share/icons/hicolor/128x128/apps/"${pkgname}".png
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/icons/hicolor/scalable/apps/"${pkgname}".svg
    # pixmaps
    install -Dm644 "${pkgdir}"/opt/"${pkgname}"/bin/"${pkgbase##*-}".svg \
                   "${pkgdir}"/usr/share/pixmaps/"${pkgname}".svg
    # licenses
    install -Dm644 "${srcdir}"/LICENSE \
                   "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

    # fix bugs:
    #
    # https://youtrack.jetbrains.com/issue/IDEA-185828
    chmod +x "${pkgdir}"/opt/"${pkgname}"/plugins/maven/lib/maven3/bin/mvn
    # https://bugs.archlinux.org/task/40934
    #sed -i 's/lcd/on/' "${pkgdir}"/opt/"${pkgname}"/bin/*.vmoptions
}

package_jetbrains-intellij-idea-jre() {
    #> Generic
    pkgdesc="JetBrains Runtime (JBR) for IntelliJ IDEA - a patched JRE"
    #> Package relations
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # mkdir
    install -dm755 "${pkgdir}"/opt/"${pkgbase}"/

    # pkg files
    rsync -rtl "${srcdir}"/opt/"${pkgbase}"/jbr "${pkgdir}"/opt/"${pkgbase}"
}
