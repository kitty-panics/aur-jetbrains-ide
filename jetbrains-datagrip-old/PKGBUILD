# Maintainer: Kitty Panics

pkgbase=jetbrains-datagrip
pkgname=(${pkgbase}-old ${pkgbase}-jre-old)
pkgver=2021.2.2
pkgrel=3
url="https://www.jetbrains.com/datagrip/download/other.html"
pkgdesc="A cross-platform Databases IDE by JetBrains."
arch=('x86_64')
license=('custom:jetbrains')
depends=('giflib' 'glib2' 'libxtst')
makedepends=('rsync')
options=('!strip')
_tar_gz="datagrip-${pkgver}.tar.gz"
install="${pkgbase}-old.install"
source=("https://download.jetbrains.com/datagrip/${_tar_gz}"
        "${pkgbase}-old.desktop"
        "${pkgbase}-old.install"
        "LICENSE")
sha256sums=('638b2c584a1b90d29e6ada829f8e3cdbfd3aad7afcd30b3a437d90b675adaaa1'
            'ffcc6de86a670c4097203b5e581efd08657b8ab93e3d49df8c38389967566e7b'
            'ec4c58e5e3636feb9ab106229bf29abe6c6117eb73f1a36fdf2a35c4e7e98fe0'
            'c37db68dbedb20617a23cf1299cd95e7f114f019ff7f44877409cef916ff6ab1')
noextract=("${_tar_gz}")

prepare() {
    install -dm755 "${srcdir}"/opt/"${pkgbase}"-old/
    bsdtar --strip-components 1 -xf "${_tar_gz}" -C "${srcdir}"/opt/"${pkgbase}"-old
}

package_jetbrains-datagrip-old() {
    provides=("${pkgname}")
    conflicts=("${pkgname}")
    backup=("opt/${pkgname}/bin/idea.properties"
            "opt/${pkgname}/bin/${pkgbase##*-}64.vmoptions")
    optdepends=('jetbrains-datagrip-jre-old: JetBrains custom Java Runtime (Recommended)'
                'java-runtime:     JRE - Required if jetbrains-datagrip-jre-old is not installed'
                'libdbusmenu-glib: For global menu support'
                'gnome-keyring:    Save login/deployment credentials safely'
                'java-openjfx:     Rendering Markdown files'
                'mariadb:          Fast SQL database server,'
                'postgresql: 	   Sophisticated object-relational DBMS'
                'sqlite:           A C library that implements an SQL database engine')

    # create dir
    install -dm755 "${pkgdir}"/opt/
    install -dm755 "${pkgdir}"/usr/bin/
    install -dm755 "${pkgdir}"/usr/share/applications/
    install -dm755 "${pkgdir}"/usr/share/doc/
    install -dm755 "${pkgdir}"/usr/share/icons/hicolor/{128x128,scalable}/apps/
    install -dm755 "${pkgdir}"/usr/share/pixmaps/
    install -dm755 "${pkgdir}"/usr/share/licenses/"${pkgname}"/

    # package files
    rsync -rtl "${srcdir}/opt" "${pkgdir}" \
          --exclude=/opt/${pkgname}/jbr \
    # /usr/bin/
    ln -s "/opt/${pkgname}/bin/${pkgbase##*-}.sh" \
          "${pkgdir}/usr/bin/${pkgname}"

    # .desktop
    install -Dm644 "${srcdir}/${pkgname}.desktop" \
                   "${pkgdir}/usr/share/applications/${pkgname}.desktop"
    # doc
    ln -s "/opt/${pkgname}/help" \
          "${pkgdir}/usr/share/doc/${pkgname}"
    # icons/hicolor
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.png" \
                   "$pkgdir/usr/share/icons/hicolor/128x128/apps/${pkgname}.png"
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.svg" \
                   "$pkgdir/usr/share/icons/hicolor/scalable/apps/${pkgname}.svg"
    # pixmaps
    install -Dm644 "${pkgdir}/opt/${pkgname}/bin/${pkgbase##*-}.svg" \
                   "${pkgdir}/usr/share/pixmaps/${pkgname}.svg"
    # licenses
    install -Dm644 "${srcdir}/LICENSE" \
                   "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

package_jetbrains-datagrip-jre-old() {
    pkgdesc='JetBrains Runtime (JBR) for DataGrip - a patched JRE'
    provides=("${pkgname}")
    conflicts=("${pkgname}")

    # create dir
    install -dm755 "${pkgdir}/opt/${pkgbase}-old/"
    # package files
    rsync -rtl "${srcdir}/opt/${pkgbase}-old/jbr" "${pkgdir}/opt/${pkgbase}-old"
}
